plugins {
    id 'java'
    id 'eclipse'
}

group 'de.unileipzig.irpact'
version '1.0-SNAPSHOT'

//sourceCompatibility = 1.11
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()

    maven {
        url "https://nexus.actoron.com/content/repositories/oss-nightlies"
    }
}

test {
    useJUnitPlatform()
}

//==================================================
//dependencies
//==================================================

ext {
    junitJupiterVersion = '5.4.2'
    junitPlatformVersion = '1.4.2'
    jacksonVersion = '2.10.2'
    joptsimpleVersion = '5.0.4'
    jadexDistVersion = '4.0.171'
    sl4jVersion = '1.7.30'
    logbackVersion = '1.2.3'
}

dependencies {
    testCompileOnly "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
    runtimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
    runtimeOnly "org.junit.vintage:junit-vintage-engine:$junitJupiterVersion"
    runtimeOnly "org.junit.platform:junit-platform-launcher:$junitPlatformVersion"
    testCompileOnly "org.apiguardian:apiguardian-api:1.0.0"

    //implementation "org.activecomponents.jadex:jadex-distribution-standard:$jadexDistVersion"
    implementation "org.activecomponents.jadex:jadex-distribution-minimal:$jadexDistVersion"
    implementation "org.activecomponents.jadex:jadex-kernel-bdiv3:$jadexDistVersion"

    implementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"

    implementation "net.sf.jopt-simple:jopt-simple:$joptsimpleVersion"

    implementation "org.slf4j:slf4j-api:$sl4jVersion"

    implementation "ch.qos.logback:logback-classic:$logbackVersion"
    implementation "ch.qos.logback:logback-core:$logbackVersion"
}

//==================================================
//jar
//==================================================

// Invalid signature file digest for Manifest main attributes -> fatjar geht (erstmal?) nicht (Problem bleibt mit shadow bestehen)
// einfach die key files entfernen...
task buildDemoFatJar(type: Jar) {
    manifest {
        attributes 'Main-class': 'de.unileipzig.irpact.start.Start'
    }
    archivesBaseName = 'FatDemo'
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } with jar
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

task buildDemoJarWithClasspath(type: Jar) {
    from sourceSets.main.output
    manifest {
        attributes 'Main-class': 'de.unileipzig.irpact.start.Start'
        attributes 'Class-Path': configurations.runtimeClasspath.files.collect { "dependencies/$it.name" }.join(' ')
    }
    archivesBaseName = 'Demo'
}

/*
task buildDemoJarWithoutClasspath(type: Jar) {
    from sourceSets.main.output
    manifest {
        attributes 'Main-class': 'de.unileipzig.irpact.start.Start'
    }
    archivesBaseName = 'Demo2'
}
*/

task copyDependencies(type: Copy) {
    into "$buildDir/dependencies"
    from configurations.runtimeClasspath
}
